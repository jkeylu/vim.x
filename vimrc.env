let s:filepath = expand('~/.vim/.env')
let s:env = { 'names': [] }
let g:vimx#env = s:env

function! s:env.load() dict
  let s:env.names = []

  if !filereadable(s:filepath)
    return
  endif

  for name in readfile(s:filepath)
    if !empty(name)
      call add(self.names, name)
    endif
  endfor
endfunction

function! s:env.exists(name) dict
  return index(self.names, a:name) >= 0
endfunction

function! s:env.add(...) dict
  if a:0 == 0
    return
  endif

  let needUpdate = 0
  for name in a:000
    if !self.exists(name)
      call add(self.names, name)
      let needUpdate = 1
    endif
  endfor

  if needUpdate
    call writefile(self.names, s:filepath)
    source ~/.vim/vimrc
  endif
endfunction

function! s:env.remove(...) dict
  if a:0 == 0
    return
  endif

  let needUpdate = 0
  for name in a:000
    let idx = index(self.names, name)
    if idx >= 0
      call remove(self.names, idx)
      let needUpdate = 1
    endif
  endfor

  if needUpdate
    call writefile(self.names, s:filepath)
    source ~/.vim/vimrc
  endif
endfunction

function! s:env.list() dict
  echo self.names
endfunction

call s:env.load()

command! -nargs=* -complete=filetype VimxEnvAdd call g:vimx#env.add(<f-args>)
command! -nargs=* -complete=filetype VimxEnvRemove call g:vimx#env.remove(<f-args>)
command! -nargs=1 -complete=filetype VimxEnvExists echo g:vimx#env.exists(<f-args>) ? 'Exists' : 'Not Exists'
command! -nargs=0 VimxEnvList call g:vimx#env.list()

" vim:ft=vim fdm=marker et ts=4 sw=2 sts=2
